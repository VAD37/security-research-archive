# No default decimal for `USD = address(840)` in `ChainlinkOracleConnector`

`ChainlinkOracleConnector` accept fixed empty `address(0)` and `address(840)`  as $ETH and $USD. This allow finding assets value in $USD instead of USDC token.

There is no fixed decimal conversion for both $ETH and $USD as token. And it completely rely on last chainlink oracle feed as decimals.

For example with 8 decimals oracle: 1 WETH-> $USD will return 3000e8.
For example with 18 decimals oracle: 1 WETH-> $USD will return 3000e18.

## Impact

Because decimal conversion is not happening for $USD and $ETH, you could say function work with this oracle feed will get wrong value.

## Proof of Concept

`ChainlinkOracleConnector` accept fixed address for token conversion into $USD value.
<https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/helpers/valueOracle/oracles/ChainlinkOracleConnector.sol#L22-L27>

```solidity
    /*
    * @notice The addresses that represents ETH and USD
    */
    address public constant ETH = address(0);
    address public constant USD = address(840);
```

When oracle was asked to convert token into $USD address, it just cancel out input token decimals.
For ex: 1 WETH -> $USD will result in `amount = WETH input * USDprice / WETH decimal= 1e18 * USDprice / 10^18 = USDprice`.

```solidity
    function getValue(address asset, address baseToken, uint256 amount) public view returns (uint256) {
        if (asset == baseToken) {
            return amount;
        }
        (address primarySource, bool isPrimaryInverse) = getSourceOfAsset(asset, baseToken);
        if (primarySource == address(0)) {
            revert NoyaChainlinkOracle_PRICE_ORACLE_UNAVAILABLE(asset, baseToken, primarySource);
        }
        address decimalsSource = isPrimaryInverse ? baseToken : asset;
        decimalsSource = decimalsSource == ETH || decimalsSource == USD ? primarySource : decimalsSource;//@ it read decimals from input token here. decimalsSource is input asset token decimal. It not possible for decimalsSource==address(0) or EOA due to config mishap.
        return getValueFromChainlinkFeed(
            AggregatorV3Interface(primarySource), amount, getTokenDecimals(decimalsSource), isPrimaryInverse
        );
    }
```

The cancel decimals in oracle happening here. There is no fixed conversion for $USD and $ETH. No decimals convert from chainlink feed oracle into something like fixed 18 decimals for $USD and $ETH.

```solidity
    function getValueFromChainlinkFeed(
        AggregatorV3Interface source, //getSourceOfAsset(asset, baseToken)
        uint256 amountIn,
        uint256 sourceTokenUnit, // isInverse ? getTokenDecimals(baseToken) : getTokenDecimals(asset)
        bool isInverse //getSourceOfAsset(asset, baseToken)
    ) public view returns (uint256) {
        int256 price;
        uint256 updatedAt;
        (, price,, updatedAt,) = source.latestRoundData(); //@could be 1e18 decimals
        uint256 uintprice = uint256(price);
        if (block.timestamp - updatedAt > chainlinkPriceAgeThreshold) {
            revert NoyaChainlinkOracle_DATA_OUT_OF_DATE();
        }
        if (price <= 0) {
            revert NoyaChainlinkOracle_PRICE_ORACLE_UNAVAILABLE(address(source), address(0), address(0));
        }
        if (isInverse) {
            return (amountIn * sourceTokenUnit) / uintprice;
        }
        return (amountIn * uintprice) / (sourceTokenUnit); //@this remove decimals from input token and depend on oracle feed decimals
    }
```

Like explained above, the $USD value have same decimals as chainlink feeds. Chainlink Feeds decimal varied between 18 and 8 for different pairs.

Currently only `GearBoxV3` connector and offchain bot using `ChainlinkOracleConnector.getValue()` with `USD=address(840)` to get TVL for its position in $USD or $ETH.

### Proof Test

Call this function in `TestAccounting.sol` between deposit/withdraw test will show the position TVL in $USD.
It show that `AccountingManager` return $USD value fixed in 8 decimals.

```solidity
    address USD = address(840);
    function debugPosition() internal {
        console.log("-----DEBUG POSITION-----");
        HoldingPI[] memory holdings = registry.getHoldingPositions(vaultId);
        for (uint256 i = 1; i < holdings.length; i++) {
            uint256 tvl = accountingManager.getPositionTVL(holdings[i], USD);
            emit log_named_bytes32("positionID:", holdings[i].positionId);
            console.log("holding [%s], TVL: %e", i, tvl);
            console.log("connector calculator: %s", holdings[i].calculatorConnector);
            console.log("connector owner: %s", holdings[i].ownerConnector);
            PositionBP memory p = registry.getPositionBP(vaultId, holdings[i].positionId);
            emit log_named_address("_calculatorConnector:", p.calculatorConnector); //@address own position
            emit log_named_uint("_positionTypeId:", p.positionTypeId); //@connector ID for positionType
            emit log_named_bytes("_trustedPositionData:", p.data); //USDC address
        }
        //holdingPosition 1: connector is AM, owner is baseConnector1
    }
```

## Tools Used

<https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/connectors/GearBoxV3.sol#L103>
<https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/helpers/valueOracle/oracles/ChainlinkOracleConnector.sol#L99>

## Recommended Mitigation Steps

There should be some custom oracle conversion  $USD and $ETH to fixed 18 decimals. This will help to get correct value for $USD and $ETH.
